#!/opt/local/bin/perl
use Mojolicious::Lite;
use Mojolicious::Validator;
use Mojolicious::Validator::Validation;
use Mojo::UserAgent;
use DateTime;
use Data::Dumper;
use DBI;

my $config  = plugin 'Config';
my $appname = "Neutrino";
my $version = "0.1";

get "/" => sub {
    my $self = shift;
    $self->render_static("index.html");
};

get "/help-about.html" => {appname => $appname, version => $version} => sub {
    my $self = shift;
} => 'about';

##########
## API ###
##########

under '/api';

get "/date" => sub {
    my $self = shift;
    my $date;
    $self->render(json => { result => 1, payload => $date });
};

get "/weather" => sub {
    my $self = shift;
    my $dbh  = _mysql_dbh($self, $config) or return;
    my $sth  = $dbh->prepare("SELECT value from configuration where name='location'");
    $sth->execute();
    my $row  = $sth->fetchrow_hashref();
    $sth->finish();
    if (exists $row->{value}) {
        my $ua = Mojo::UserAgent->new();
        $ua->max_redirects(0)->connect_timeout(6)->request_timeout(10);
        my $tx = $ua->get("http://api.openweathermap.org/data/2.5/weather?q=$row->{value}" => {DNT => 1});
        if (my $res = $tx->success) { 
            $self->render(json => { result => 1, payload => $res->body}); 
        } else {
            my $err = $tx->error;
            $self->render(json => { result => 0, text => 'Unable to fetch weather from openweathermap.org' }, status => $err->{code});
        }
    } else {
        $self->render(json => { result => 0, text => 'no location config found, no weather queried' }, status => 404);
    }
};

get "/sensors" => sub {
    my $self = shift;
    my $dbh  = _mysql_dbh($self, $config) or return;
    my $sth  = $dbh->prepare("SELECT * from sensor");
    $sth->execute();
    my $data = $sth->fetchall_arrayref({});
    $sth->finish();
    if (!$dbh->errstr()) {
        $self->render(status => 200, json => { result => 1, payload => $data});
    } else {
        $self->render(status => 500, json => { result => 0, payload => 'Database error occurred'});
    }
};

get "/controllers" => sub {
    my $self = shift;
    my $dbh  = _mysql_dbh($self, $config) or return;
    my $sth  = $dbh->prepare("SELECT * from controller");
    $sth->execute();
    my $data = $sth->fetchall_hashref('id');
    $sth->finish();
    if (!$dbh->errstr()) {
        $self->render(status => 200, json => { result => 1, payload => $data});
    } else {
        $self->render(status => 500, json => { result => 0, payload => 'Database error occurred:' . $dbh->errstr()});
    }
};

get "/sensorgroups" => sub {
    my $self = shift;
    my $dbh  = _mysql_dbh($self, $config) or return;
    my $sth  = $dbh->prepare("SELECT * from sensorgroup");
    $sth->execute();
    my $data = $sth->fetchall_hashref('id');
    $sth->finish();
    if (!$dbh->errstr()) {
        # insert group members as well
        foreach my $key (keys %{$data}) {
            $sth = $dbh->prepare("SELECT sensor_address,display_name,sensor_group,sensor.id,sensorgroup_id " .
                                    "from sensor,sensor_tie where sensor_tie.sensor_id = sensor.id and sensor_tie.sensorgroup_id=$key");
            $sth->execute();
            if(!$dbh->errstr()) {
                $data->{$key}->{members} = $sth->fetchall_hashref('id');
            }
            $sth->finish();
        }
        $self->render(status => 200, json => { result => 1, payload => $data});
    } else {
        $self->render(status => 500, json => { result => 0, payload => 'Database error occurred'});
    }
};

put "/sensorgroups" => sub {
    my $self       = shift;
    my $name       = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'name' => $name});
    $validation->required('name')->like(qr/^[\s\w\d,\-\._\']+$/);;
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "name provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    $dbh->do("insert into sensorgroup (display_name) values (?)", undef, $name);
    if($dbh->errstr()) {
        $self->render(status => 500, json => { result => 0, text => 'failed to save values to database'});
    }
    $self->render(status => 200, json => { result => 1, text => 'success' });
   
};

del "/sensorgroups/:id" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id});
    $validation->required('id')->like(qr/^\d+$/);;
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "id provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    $dbh->do("delete from sensorgroup where id=?", undef, $id);
    if($dbh->errstr()) {
        if ($dbh->errstr() =~ /foreign key constraint fails/) {
            $self->render(status => 500, json => { result => 0, text => 'Cannot delete group with sensors attached'});
        } else {
            $self->render(status => 500, json => { result => 0, text => 'failed to delete from database:' . $dbh->errstr()});
        }
    } else {
        $self->render(status => 200, json => { result => 1, text => 'success' });
    }
};

post "/sensorgroups/:id/name" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $name       = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id, 'name' => $name});
    $validation->required('id')->like(qr/^\d+$/);
    $validation->required('name')->like(qr/^[\s\w\d,\-\._\']+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "id or value provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    $dbh->do("update sensorgroup set display_name=? where id=?", undef, $name, $id);
    if($dbh->errstr()) {
        $self->render(status => 500, json => { result => 0, text => 'failed to update database:' + $dbh->errstr()});
    }
    $self->render(status => 200, json => { result => 1, text => 'success' });
};

get "/sensorgroups/:id/sensors" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id});
    $validation->required('id')->like(qr/^\d+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "id or value provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    my $sth = $dbh->prepare("SELECT sensor_address,display_name,sensor_group,sensor.id,sensorgroup_id " .
                            "from sensor,sensor_tie where sensor_tie.sensor_id = sensor.id and sensor_tie.sensorgroup_id=$id");
    $sth->execute();
    if(!$dbh->errstr()) {
        my $data = $sth->fetchall_arrayref({});
        $self->render(status => 200, json => { result => 1, payload => $data});
    } else {
        $self->render(status => 500, json => { result => 0, text => 'failed to query sensors for sensor group'});
    }
};

put "/sensorgroups/:id/sensors/:value" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $sensorid   = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id, 'sensorid' => $sensorid});
    $validation->required('id')->like(qr/^\d+$/);
    $validation->required('sensorid')->like(qr/^\d+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "id or value provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    # does relationship already exist?
    my $sth = $dbh->prepare("SELECT * FROM sensor_tie WHERE sensor_id=$sensorid AND sensorgroup_id=$id");
    $sth->execute();
    if(!defined($sth->fetchrow_arrayref())) {
        $dbh->do("INSERT INTO sensor_tie (sensor_id, sensorgroup_id) VALUES (?, ?)", undef, $sensorid, $id);
        if (!$dbh->errstr()) {
            $self->render(status => 200, json => { result => 1, text => "success"});
        } else {
            $self->render(status => 500, json => { result => 0, text => "unable to insert into database"});
        }
    } else {
        # it already exists
        $self->render(status => 200, json => { result => 1, text => "success"});
    }
};

del "/sensorgroups/:id/sensors/:value" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $sensorid   = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id, 'sensorid' => $sensorid});
    $validation->required('id')->like(qr/^\d+$/);
    $validation->required('sensorid')->like(qr/^\d+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "id or value provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    $dbh->do("DELETE FROM sensor_tie WHERE sensor_id=? and sensorgroup_id=?", undef, $sensorid, $id);
    if (!$dbh->errstr()) {
        $self->render(status => 200, json => { result => 1, text => "success"});
    } else {
        $self->render(status => 500, json => { result => 0, text => "unable to delete from database"});
    }

};

get "/sensors/:id/data" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $value      = $self->param('value');
    my $hours      = $self->param('hours');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id, 'value' => $value, 'hours' => $hours});
    $validation->required('id')->like(qr/^\d+$/);
    $validation->required('value')->in(qw(voltage temperature humidity pressure all));
    $validation->required('hours')->size(1,720);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "a parameter($id,$value,$hours) provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    my $field = $value;
    $field = "voltage,pressure,humidity,temperature" if $field eq "all";
    my $sth = $dbh->prepare("SELECT UNIX_TIMESTAMP(date) as epoch,date,id,sensor_address,$field from data where sensor_address=$id and date >= DATE_SUB(NOW(),INTERVAL $hours HOUR)");
    $sth->execute();
    my $data = $sth->fetchall_arrayref({});
    $sth->finish();
    if (!$dbh->errstr()) {
        $self->render(status => 200, json => { result => 1, payload => $data});
    } else {
        print $dbh->errstr();
        $self->render(status => 500, json => { result => 0, text => 'Database error occurred'});
    }
};

post "/sensors/:id/name" => sub {
    my $self       = shift;
    my $id         = $self->param('id');
    my $value      = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'id' => $id, 'value' => $value});
    $validation->required('id')->like(qr/^\d+$/);
    $validation->required('value')->like(qr/^[\s\w\d,\-\._\']+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "a parameter($id,$value) provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    $dbh->do("UPDATE sensor set display_name=? where sensor_address=?", undef, $value, $id);
    if($dbh->errstr()) {
        $self->render(status => 500, json => { result => 0, text => 'failed to save value to database'});
    }
    $self->render(status => 200, json => { result => 1, text => 'success' });
};

get "/config" => sub {
    my $self = shift;
    my $dbh = _mysql_dbh($self, $config) or return;
    my $sth = $dbh->prepare("SELECT name,value from configuration");
    $sth->execute();
    my $data = $sth->fetchall_hashref('name');
    $sth->finish();
    $self->render(json => { result => 1, payload => $data});
};

get "/config/:key" => sub {
    my $self = shift;
    my $key = $self->param('key');
    my $dbh = _mysql_dbh($self, $config) or return;
    my $sth = $dbh->prepare("SELECT value from configuration where name='$key'");
    $sth->execute();
    my $row = $sth->fetchrow_hashref();
    $sth->finish();
    if (exists $row->{value}) {
        $self->render(json => { result => 1, payload => $row});
    } else {
        $self->render(json => { result => 0, text => 'no config found' }, status => 404);
    }
};

post "/config/:key" => sub {
    my $self       = shift;
    my $key        = $self->param('key');
    my $value      = $self->param('value');
    my $validator  = Mojolicious::Validator->new;
    my $validation = Mojolicious::Validator::Validation->new(validator => $validator);

    $validation->input({'key' => $value});
    $validation->required('key')->like(qr/^[\s\w\d,\-\._\']+$/);
    if(! $validation->is_valid) {
        $self->render(status => 406, json => { result => 0, text => "value '$value' provided did not match character validation"} );
        return;
    }

    my $dbh = _mysql_dbh($self, $config) or return;
    my $sth = $dbh->prepare("select id from configuration where name='$key'");
    $sth->execute();
    my $row = $sth->fetchrow_hashref();
    if (exists $row->{id}) {
        $dbh->do("UPDATE configuration SET name=?, value=? WHERE id=?", undef, $key, $value, $row->{id});
        if($dbh->errstr()) {
            $self->render(status => 500, json => { result => 0, text => 'failed to save value to database'});
            return;
        }
    } else {
        $dbh->do("INSERT INTO configuration (name, value) VALUES (?, ?)", undef, $key, $value);
        if($dbh->errstr()) {
            $self->render(status => 500, json => { result => 0, text => 'failed to save value to database'});
            return;
        }
    }

    $self->render(status => 200, json => { result => 1, text => 'success' });
};


app->start;


##################
### local subs ###
##################

sub _mysql_dbh {
    my $self   = shift;
    my $config = shift;
    my $dbh    = DBI->connect("DBI:mysql:database=$config->{mysqldb}:host=$config->{mysqlserver}:port=3306",
                           $config->{mysqluser},$config->{mysqlpass})
                      or $self->render(status=>500,text=>'Failed database connection') and return 0;
    return $dbh;
}

__DATA__

@@ about.html.ep
<div class="jumbotron">
  <div class="container">
    <%= $appname %> <%= $version %>
  </div>
</div>
